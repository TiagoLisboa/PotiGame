tipo Jogador
	var x, y: Real
	var l, h: Inteiro
	var sprite: Imagem
	var mapa: Mapa
	var camera: Camera
	vel = 50
	var caixa_colisao = Caixa(
			Ponto(x, y), 
			Ponto(x + l, y),
			Ponto(x, y + h),
			Ponto(x + l, y + h))

	iniciar() fim

	desenhar() 
		sprite.desenhe(x + camera.ox, y + camera.oy, 3)
	fim

	atualizar(ds: Real) 
		var nx = 0.0
		var ny = 0.0

		se Teclado.TECLA_W então ny := - (vel * ds) fim
		se Teclado.TECLA_S então ny := (vel * ds) fim
		se Teclado.TECLA_A então nx := - (vel * ds) fim
		se Teclado.TECLA_D então nx := (vel * ds) fim

		var colidiu = caixa_colisao.checar_posicao(mapa, Caixa(
			Ponto(x + nx, y + ny), 
			Ponto(x + nx + l, y + ny),
			Ponto(x + nx, y + ny + h),
			Ponto(x + nx + l, y + ny + h)))

		se 
			colidiu == falso
		então
			x := x + nx
			y := y + ny
		senão
			nx := 0.0
			ny := 0.0
		fim

		caixa_colisao := Caixa(
			Ponto(x, y), 
			Ponto(x + l, y),
			Ponto(x, y + h),
			Ponto(x + l, y + h))

		camera.deslocar(nx, ny, x + camera.ox, y + camera.oy)

	fim
fim