tipo Jogador
  # construtor
	var x, y: Real
	var l, h: Inteiro
	var sprite: Imagem
	var mapa: Mapa
	#var inimigos: Lista[Inimigo]

	# atributos
	var venceu = falso
	vel = 50
	var caixa_colisao := Caixa(x, y, l, h)

  # metodos
	# init = para i de 1 até 1 faça iniciar() fim
	iniciar() fim

	checar_colisao(box: Caixa) = box.checar_colisao(caixa_colisao)

	desenhar(camera: Camera)
		sprite.desenhe(x - camera.ox, y - camera.oy, 3)
	fim

	atualizar(ds: Real)
		var nx = 0.0
		var ny = 0.0

		se      Teclado.TECLA_W então ny := - (vel * ds)
		senãose Teclado.TECLA_S então ny := (vel * ds)
		senãose Teclado.TECLA_A então nx := - (vel * ds)
		senãose Teclado.TECLA_D então nx := (vel * ds) fim

		var teste = caixa_colisao.checar_posicao(mapa, Caixa(x + nx, y + ny, l, h))

		colidiu = teste.primeiro
		venceu := teste.segundo

		se colidiu então
			nx := 0.0
			ny := 0.0
		senão
			x := x + nx
  		y := y + ny
		fim

		caixa_colisao := Caixa(x, y, l, h)
	fim
fim
